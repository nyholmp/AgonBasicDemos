   10 REM TWISTM20.BAS
   20 :
   30 REM WRITTEN BY PETE NYHOLM
   40 REM CREDIT FOR PROC_LOAD_LOGO AND PROC_DRAW_LOGO TO STEVE LOVEJOY
   50 REM CODE BASED ON ORIGINAL BOOTSCREEN.BAS VERSION 2.0
   60 REM CREDIT FOR ICON TO ARNOLD MESZAROS
   70 REM TESTED FOR MODE 0
   80 REM LOAD LOGO AS HEX DATA AND LOAD INTO STRING ARRAY
   90 :
  100 DIM L$(7) : REM INITIALIZE LOGO STRING ARRAY 
  110 FOR LL%=0 TO 7 : L$(LL%)="" : FOR LC%=1 TO 6 
  120     READ V% : L$(LL%)=L$(LL%)+CHR$(V%)
  130   NEXT LC% : NEXT LL%
  140 :
  150 DATA 032,032,200,201,032,032
  160 DATA 032,032,202,203,032,032
  170 DATA 032,204,205,206,207,032
  180 DATA 208,209,210,211,212,213
  190 DATA 214,215,216,209,217,218
  200 DATA 219,220,221,222,223,224
  210 DATA 225,226,227,228,229,230
  220 DATA 231,232,233,234,235,236
  230 :
  240 REM DECLARE ARRAYS FOR COMPUTED POLYGON VERTICES UP TO 12 SIDES
  250 DIM PX(12), PY(12)
  260 REM DECLARE ARRAYS FOR SIN AND COS TO 0.1 DEG, NEED 10*360 X 1 LOOKUP TABLE
  270 DIM LCOS(3600), LSIN(3600)
  280 REM SET MAXIMUM COLORS TO USE FOR MODE 0
  290 MAXC%=63
  300 BGCOL%=0 : COLOUR 128+BGCOL%
  310 MODE 20 : CLS
  320 PROC_LOAD_LOGO
  330 LCOL%=23 : LROW%=20 : REM CENTERS LOGO FOR MODE 20
  340 PROC_DRAW_LOGO(LCOL%-10,LROW%-2,4)
  350 PRINT TAB(30,40)"COMPUTING SIN, COS ARRAYS";
  360 REM SHOW FUN CIRCULAR PATTERN WHEN COMPUTING SIN AND COS
  370 R=100 : REM RADIUS OF PATTERN
  380 W=10 : REM WIDTH OF PATTERN
  390 FOR A%=0 TO 3600
  400   LCOS(A%)=COS(RAD(A%/10))
  410   LSIN(A%)=SIN(RAD(A%/10))
  420   IF (A% MOD 10) <> 0 THEN GOTO 460
  430   GCOL 0,A%/10 MOD (MAXC% + 1)
  440   PLOT 4,512+R*LCOS(A%),512+R*LSIN(A%)
  450   PLOT 5,512+(R+W)*LCOS(A%),512+(R+W)*LSIN(A%)
  460   R=R+LSIN(A%)/4
  470   W=W+LCOS(A%)/4
  480 NEXT A%
  490 MAXX%=1024
  500 APER%=128 : REM MIN APERATURE FOR CENTER
  510 MAXST%=30 : REM DEFAULT IS 30
  520 MINSC=0.2 : REM DEFAULT IS 0.2
  530 MAXSI%=8 : REM DEFAULT IS 8
  540 CLS
  550 REM RANDOMIZE ANGLE SIZE BETWEEN POLYGON VERTICES
  560 ASTEP%=10*(1+RND(MAXST%-1)) : REM RANDOM ANGLE UP TO MAXSTEP
  570 REM RANDOMIZE SCALING FACTOR FOR EACH SUCCESSIVE POLYGON IN PATTERN 
  580 SCALE=1-MINSC+RND(MINSC*100-1)/100
  590 REM LOOP FOR NUMBER OF SIDES FOR EACH POLYGON (MIN 2) : MIN 3 FOR LOGO
  600 FOR S%=3 TO MAXSI%
  610   FOR C%=0 TO MAXC%
  620     COLOUR C% 
  630     D%=C% DIV 16 : PRINT TAB(52+3*D%,27+C%-16*D%);"";C%;
  640   NEXT C%
  650   REPEAT LCOLOR%=1+RND(14) : UNTIL LCOLOR% <> BGCOL% 
  660   PROC_DRAW_LOGO(LCOL%,LROW%,LCOLOR%)
  670   PRINT TAB(43,1)"HIT KEY DURING PAUSE";
  675   PRINT TAB(43,2)"TO CHANGE";
  680   PRINT TAB(46,3)"B: BG COLOR";
  690   PRINT TAB(46,4)"A: MAX ANGLE";
  700   PRINT TAB(46,5)"S: SCALE";
  710   PRINT TAB(46,6)"N: MAX POLY SIDES";
  720   PRINT TAB(46,7)"Q: QUIT";
  730   PRINT TAB(52,11)"ANGLE=";ASTEP%/10
  740   PRINT TAB(52,12)"SCALE=";SCALE;
  750   PRINT TAB(52,13)"BGCOL=";BGCOL%;
  760   PRINT TAB(52,14)"SIDES=";S%;
  761   PRINT TAB(52,15)"MAX ANG=";MAXST%;
  770   PRINT TAB(52,26)"COLORS";
  780   REM CALCULATE ANGLE IN 10THS OF DEG FOR NUMBER OF SIDES
  790   ASIDE=3600/S% : C%=0 : X%=MAXX%/2
  800   FOR A%=0 TO ASIDE STEP ASTEP%
  810     REPEAT C%=C%+1 UNTIL C% <> BGCOL% : REM AVOID BACKGROUND COLOR
  820     GCOL 0,C%
  830     PROC_POLY(X%,MAXX%,A%,S%)
  840     IF C%>MAXC% THEN C%=0
  850   NEXT A%
  860   REM COMPUTE NEXT X VALUE SCALED FROM PREVIOUS UNTIL MIN OF APER%
  870   X%=X%*SCALE
  880   IF X% >= APER% + 54/(S%-2) THEN GOTO 800
  890   REM INTRODUCE DELAY BETWEEN EACH NEW NUMBER OF SIDES
  900   K=INKEY(100)
  910   IF K=66 THEN INPUT TAB(1,40)"ENTER NEW BACKGROUND COLOR",BC$ : BGCOL%=VAL(BC$) : COLOUR 128+BGCOL%
  920   IF K=65 THEN INPUT TAB(1,40)"ENTER NEW MAX ANGLE STEP",MAS$ : MAXST%=VAL(MAS$)
  930   IF K=83 THEN INPUT TAB(1,40)"ENTER SCALE 1-10",SC$ : MINSC=(11-VAL(SC$))/20
  940   IF K=78 THEN INPUT TAB(1,40)"MAX POLYGON SIDES 3-12",MS$ : MAXSI%=VAL(MS$)
  950   IF K=81 THEN GOTO 990
  960   CLS
  970 NEXT S%
  980 GOTO 550
  990 PRINT "THANKS FOR TRYING THE PROGRAM."
 1000 END
 1010 REM DEFINE PROCEDURE TO PLOT POLYGON
 1020 REM GIVEN RADIUS, MAX X, STARTING ANGLE, AND NUMBER OF SIZES
 1030 DEF PROC_POLY(XP%,MAXXP%,AP,NS%)
 1040 CENTER%=MAXXP%/2
 1050 FOR I%=1 TO NS%
 1060   ADP%=INT(AP+(I%-1)*ASIDE)
 1070   PX(I%)=CENTER%-XP%*LCOS(ADP%)+8
 1080   PY(I%)=CENTER%-XP%*LSIN(ADP%)
 1090   IF I% = 1 THEN PLOT 4,PX(1),PY(1) ELSE PLOT 5,PX(I%),PY(I%)
 1100 NEXT I%
 1110 PLOT 5,PX(1),PY(1)
 1120 ENDPROC
 1130 :
 1140 DEF PROC_DRAW_LOGO(COL%,ROW%,LC%) : REM COLUMN, ROW, COLOR
 1150 REM DISPLAY THE LOGO LINE BY LINE FROM LOGO ARRAY
 1160 COLOUR LC%
 1170 FOR I%=0 TO 7 : PRINT TAB(COL%,ROW%+I%);L$(I%); : NEXT I%
 1180 ENDPROC
 1190 :
 1200 DEF PROC_LOAD_LOGO
 1210 REM AGON LIGHT "Sitting Kung Fu man" LOAD LOGO CHARS
 1220 FOR AC%=200 TO 237 : VDU 23,AC%
 1230   FOR I%=1 TO 8 : READ V$ : H%=EVAL("&"+V$) : VDU H% : NEXT I%
 1240 NEXT AC%
 1250 ENDPROC
 1260 REM HEX VALUES FOR CUSTOM LOGO CHARACTERS TO BE READ INTO VDU
 1270 DATA 07,0F,1F,1F,1F,1F,1F,3F, C0,E0,F0,F0,F0,F0,F0,F8
 1280 DATA 3F,3F,1F,0F,0F,0F,07,07, F8,F8,F0,E0,E0,E0,C0,C0
 1290 DATA 00,01,07,0F,1F,3F,7F,7F, 07,C7,C7,C3,E3,E1,F0,F0
 1300 DATA C0,C3,C3,87,87,0F,0F,1F, 00,00,E0,F0,F8,FC,FC,FE
 1310 DATA 00,00,01,01,03,03,07,07, FF,FF,FF,FF,FF,FF,FF,FF
 1320 DATA F8,F8,F8,F0,E0,E1,C1,C3, 3F,3F,7F,FF,FF,FF,FF,FF
 1330 DATA FF,FF,FF,FF,FF,FF,DF,DF, 00,00,80,80,80,C0,C0,C0
 1340 DATA 07,07,0F,0F,0F,0F,0F,1F, FF,F7,E7,E7,E7,C7,87,82
 1350 DATA C7,87,87,8F,0F,0F,1F,1F, CF,CF,CF,C7,C7,C3,81,01
 1360 DATA E0,E0,E0,F0,F0,F0,F0,F0, 1F,1F,1F,1F,0F,0F,0F,1F
 1370 DATA 80,80,80,80,81,8F,87,8F, 1F,3F,0F,00,80,E0,E0,C3
 1380 DATA FC,F0,80,00,03,07,23,F3, 01,01,01,81,C1,E1,F1,F1
 1390 DATA F0,F0,F0,F0,F0,F0,F0,F0, 1F,1F,03,03,03,03,07,0F
 1400 DATA DF,FF,FF,FF,FF,FF,FE,FE, C7,87,87,07,0F,0F,1F,1F
 1410 DATA F1,F1,F1,F8,F8,F8,F8,F8, FD,FF,FF,FF,FF,FF,FF,7F
 1420 DATA F0,F8,F8,E0,E0,E0,F0,F8, 1F,3F,3F,3F,1F,1F,0F,00
 1430 DATA FE,FC,FC,FC,F8,F8,E0,00, 1F,3C,20,00,00,00,00,00
 1440 DATA 78,1C,0C,0C,00,00,00,00, 7F,7F,7F,7F,7F,3F,0F,00
 1450 DATA F8,FC,FC,FC,FC,F8,F0,00, FF,FE,FC,F8,F0,E0,C0,80
